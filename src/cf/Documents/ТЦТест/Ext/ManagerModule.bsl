
Процедура ВывестиПараметрыТеста(ТабДок, Ссылка) Экспорт

	Макет = Документы.ТЦТест.ПолучитьМакет("ПараметрыТеста");
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТЦТест.Дата КАК Дата,
	|	ТЦТест.Прерван КАК Прерван,
	|	ТЦТест.Результат КАК Результат,
	|	ТЦТест.Сценарий КАК Сценарий,
	|	ТЦТест.ДатаОкончания КАК ДатаОкончания,
	|	ТЦТест.ТекущаяИтерация КАК ТекущаяИтерация,
	|	ТЦТест.ОбщееКоличествоОдновременноЗапускаемыхВРМ КАК ОбщееКоличествоОдновременноЗапускаемыхВРМ,
	|	ТЦТест.КоличествоОдновременноЗапускаемыхВРМАгента КАК КоличествоОдновременноЗапускаемыхВРМАгента,
	|	ТЦТест.НачальноеКоличествоПользователей КАК НачальноеКоличествоПользователей,
	|	ТЦТест.НачальныйШагДозапускаПользователей КАК НачальныйШагДозапускаПользователей,
	|	ТЦТест.КонечныйШагДозапускаПользователей КАК КонечныйШагДозапускаПользователей,
	|	ТЦТест.КритическоеКоличествоОшибокИтерации КАК КритическоеКоличествоОшибокИтерации,
	|	ТЦТест.КритическоеКоличествоОшибокТеста КАК КритическоеКоличествоОшибокТеста,
	|	ТЦТест.МаксимальноеКоличествоИтераций КАК МаксимальноеКоличествоИтераций,
	|	ТЦТест.ПороговыйAPDEX КАК ПороговыйAPDEX,
	|	ТЦТест.КоэффициентИнтенсивности КАК КоэффициентИнтенсивности,
	|	ТЦТест.ЭтоДинамическийТест КАК ЭтоДинамическийТест,
	|	ТЦТест.АдресПубликации КАК АдресПубликации,
	|	ТЦТест.ЕстьРазделение КАК ЕстьРазделение,
	|	ТЦТест.НомерСеансаФоновогоЗадания КАК НомерСеансаФоновогоЗадания,
	|	ТЦТест.НомерСоединенияФоновогоЗадания КАК НомерСоединенияФоновогоЗадания,
	|	ТЦТест.Менеджер КАК Менеджер,
	|	ТЦТест.ТаблицаРаспределения.(
	|		НомерСтроки КАК НомерСтроки,
	|		Компьютер КАК Компьютер,
	|		Вес КАК Вес
	|	) КАК ТаблицаРаспределения
	|ИЗ
	|	Документ.ТЦТест КАК ТЦТест
	|ГДЕ
	|	ТЦТест.Ссылка В(&Ссылка)";
	
	Запрос.Параметры.Вставить("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать();

	ОбластьЗаголовок = Макет.ПолучитьОбласть("Заголовок");
	
	Шапка = Макет.ПолучитьОбласть("Шапка");
	ШапкаМодификацииСценария= Макет.ПолучитьОбласть("ШапкаМодификацииСценария");
	
	ОбластьТаблицаРаспределенияШапка = Макет.ПолучитьОбласть("ТаблицаРаспределенияШапка");
	ОбластьТаблицаРаспределения = Макет.ПолучитьОбласть("ТаблицаРаспределения");
	ТабДок.Очистить();

	Пока Выборка.Следующий() Цикл
		
		ОбластьЗаголовок.Параметры.Дата = Формат(Выборка.Дата, "ДЛФ=DT");
		ОбластьЗаголовок.Параметры.ДатаОкончания = Формат(Выборка.ДатаОкончания, "ДЛФ=DT");
		ТабДок.Вывести(ОбластьЗаголовок);

		Шапка.Параметры.Заполнить(Выборка);
		Шапка.Параметры.Результат = НРег(Шапка.Параметры.Результат);
		ТабДок.Вывести(Шапка, Выборка.Уровень());
		
		Если Не ПустаяСтрока(Выборка.АдресПубликации) Или Выборка.ЕстьРазделение Тогда
			
			ШапкаМодификацииСценария.Параметры.Заполнить(Выборка);
			ТабДок.Вывести(ШапкаМодификацииСценария, Выборка.Уровень());
			
		КонецЕсли;

		ВыборкаТаблицаРаспределения = Выборка.ТаблицаРаспределения.Выбрать();
		Если ВыборкаТаблицаРаспределения.Количество() > 0 Тогда
			
			ТабДок.Вывести(ОбластьТаблицаРаспределенияШапка);
			
			Пока ВыборкаТаблицаРаспределения.Следующий() Цикл
				ОбластьТаблицаРаспределения.Параметры.Заполнить(ВыборкаТаблицаРаспределения);
				ТабДок.Вывести(ОбластьТаблицаРаспределения, ВыборкаТаблицаРаспределения.Уровень());
			КонецЦикла;
			
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры
